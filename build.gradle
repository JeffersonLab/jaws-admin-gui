plugins {
    id 'war'
    id ("com.github.node-gradle.node") version "3.2.1"
}

description 'Jefferson Lab Alarm Warning System (JAWS) web admin interface'
group 'org.jlab'
version '1.3.0'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

node {
    download = true
}

repositories {
    mavenCentral()

    maven {
        url "https://packages.confluent.io/maven"
    }

    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
}

dependencies {
    if (project.hasProperty("provided")) {
        providedCompile 'org.jlab:jaws-libj:1.9.0',
                'io.confluent:kafka-streams-avro-serde:7.0.1',
                'com.fasterxml.jackson.core:jackson-databind:2.13.1',
                'org.tuckey:urlrewritefilter:4.0.4'
    } else {
        implementation 'org.jlab:jaws-libj:1.9.0',
                'io.confluent:kafka-streams-avro-serde:7.0.1',
                'com.fasterxml.jackson.core:jackson-databind:2.13.1',
                'org.tuckey:urlrewritefilter:4.0.4'
    }

    providedCompile 'javax:javaee-api:8.0.1'
}

war {
    dependsOn('npmInstall')

    archiveFileName =  'jaws-admin-gui.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
    }

    from('node_modules/page/page.mjs') {
        into('resources/libs/page-1.11.6')
    }

    from('node_modules/dexie/dist/modern/dexie.min.mjs') {
        into('resources/libs/dexie-3.2.1')
    }
}

tasks.war.doLast {
    copy {
        from war
        into './run'
    }
}
