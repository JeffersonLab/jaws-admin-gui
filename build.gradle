plugins {
    id 'war'
}

description 'Jefferson Lab Alarm Warning System (JAWS) web admin interface'
group 'org.jlab'
version new File("${projectDir}/VERSION").text.trim()
ext.releaseDate = new Date().format('MMM dd yyyy')
// IndexDB Client-side cache data version
ext.cacheVersion = '1'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()

    maven {
        url "https://packages.confluent.io/maven"
    }

    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
}

configurations {
    jbossModule
    providedCompile.extendsFrom(jbossModule)
}

dependencies {
    implementation 'org.jlab:smoothness-weblib:4.5.0'

    // Smoothness setup should install these into Wildfly as global module
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4'

    // Wildfly 26.1.3.Final provides globally:
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2'

    // A custom JBoss Module
    jbossModule 'org.jlab:jaws-libj:4.8.0',
                'io.confluent:kafka-avro-serializer:7.4.0'

    testImplementation 'junit:junit:4.13.2',
                       'com.oracle.database.jdbc:ojdbc11:21.7.0.0'
}

sourceSets {
    integration {
        java.srcDir "${projectDir}/src/integration/java"
        resources.srcDir "${projectDir}/src/integration/resources"
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    testLogging {
        showStandardStreams = true
    }

    environment 'ORACLE_USER', 'JAWS_OWNER'
    environment 'ORACLE_PASS', 'password'
    environment 'ORACLE_SERVICE', 'xepdb1'
    environment 'ORACLE_SERVER', 'localhost:1521'
}

war {
    archiveFileName = 'jaws.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }

    filesMatching('worker.mjs') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
    }

    filesMatching('resources/modules/jaws-admin-gui/db.mjs') {
        filter {
            String line -> line.replaceAll("@CACHE_VERSION@", cacheVersion)
        }
    }
}